prefix:
{
  config,
  lib,
  libEx,
  flake,
  ...
}:
let
  cfg = config.${prefix}.home;
  cfgHome = config.home-manager.users.${cfg.username};
in
{
  options.${prefix}.home = {
    enable = lib.mkEnableOption "home config" // {
      default = true;
    };
    restoreBackups = lib.mkEnableOption "restore backup files before activation";
    username = lib.mkOption {
      description = "Main username.";
      type = lib.types.str;
      default = "znaniye";
    };
    extraModules = lib.mkOption {
      description = "Extra modules to import.";
      type = with lib.types; coercedTo attrs (x: [ x ]) (listOf attrs);
      default = [ ];
    };
  };

  config = lib.mkIf cfg.enable {
    # Home-Manager standalone already adds home-manager to PATH, so we
    # are adding here only for NixOS
    environment.systemPackages = [
      cfgHome.programs.home-manager.package
    ];

    home-manager = {
      useUserPackages = true;
      useGlobalPkgs = true;
      users.${cfg.username} = {
        inherit (config) meta theme;
        imports = [ flake.outputs.homeModules.default ] ++ cfg.extraModules;
        home-manager = {
          inherit (config.networking) hostName;
        };
      };
      extraSpecialArgs = { inherit flake libEx; };
    };
  };
}
